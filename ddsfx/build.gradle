plugins {
    id 'org.openjfx.javafxplugin'
}

dependencies {
    api project(':ddsiio')
}

javafx {
    version = '21.0.6'
    modules = ['javafx.graphics']
    configurations = ['compileOnly', 'testImplementation']
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ['--add-exports', 'javafx.graphics/com.sun.javafx.iio=io.github.ititus.ddsfx',
                             '--add-exports', 'javafx.graphics/com.sun.javafx.iio.common=io.github.ititus.ddsfx']
}

tasks.withType(Javadoc).configureEach {
    options.addMultilineStringsOption('-add-exports').setValue(['javafx.graphics/com.sun.javafx.iio=io.github.ititus.ddsfx',
                                                                'javafx.graphics/com.sun.javafx.iio.common=io.github.ititus.ddsfx'])
}

mavenPublishing {
    publishToMavenCentral(true)
    signAllPublications()

    pom {
        name = 'DDS FX Reader'
        description = 'DDS Image Library for JavaFX'
        url = 'https://github.com/iTitus/dds'
        licenses {
            license {
                name = 'MIT License'
                url = 'https://mit-license.org'
            }
        }
        developers {
            developer {
                id = 'iTitus'
                name = 'iTitus'
                email = 'ititus@users.noreply.github.com'
            }
        }
        scm {
            connection = 'scm:git:git://github.com/iTitus/dds.git'
            developerConnection = 'scm:git:ssh://git@github.com:iTitus/dds.git'
            url = 'https://github.com/iTitus/dds'
            tag = "v${version}"
        }
    }
}
