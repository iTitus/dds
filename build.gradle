plugins {
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
    id 'org.openjfx.javafxplugin' version '0.0.13' apply false
    // id 'org.javamodularity.moduleplugin' version '1.8.12' apply false
}

nexusPublishing {
    connectTimeout = clientTimeout = Duration.ofHours(2)
    repositories {
        sonatype {
            stagingProfileId = project.findProperty('ossrh_staging_profile_id')
            username = project.findProperty('ossrh_username')
            password = project.findProperty('ossrh_password')
        }
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'java-library-distribution'
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    // apply plugin: 'org.javamodularity.moduleplugin'

    group = 'io.github.ititus'
    version = '1.0.0'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }

        withSourcesJar()
        withJavadocJar()
    }

    /*modularity {
        improveEclipseClasspathFile()
        // disableEffectiveArgumentsAdjustment()
    }*/

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    tasks.withType(Test) {
        systemProperty 'file.encoding', 'UTF-8'
    }

    javadoc {
        options {
            addStringOption('Xdoclint:all,-missing', '-quiet')
        }
    }

    repositories {
        mavenCentral()
    }

    test {
        useJUnitPlatform()
        testLogging {
            events 'PASSED', 'FAILED', 'SKIPPED'
        }
    }
}
