plugins {
    id 'com.vanniktech.maven.publish' version '0.34.0' apply false
    id 'org.openjfx.javafxplugin' version '0.1.0' apply false
}

allprojects {
    tasks.register('publishSnapshotToMavenCentral') {
        if (version.endsWith('-SNAPSHOT') && tasks.findByName('publishToMavenCentral')) {
            dependsOn publishToMavenCentral
        }
    }.configure {
        onlyIf('SNAPSHOT version') {
            version.endsWith('-SNAPSHOT') && tasks.findByName('publishToMavenCentral')
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.vanniktech.maven.publish'

    group = 'io.github.ititus'
    version = '4.0.1-SNAPSHOT'

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.release = 21
    }

    tasks.withType(Test).configureEach {
        systemProperty 'file.encoding', 'UTF-8'
    }

    javadoc {
        options {
            // silence warnings on missing javadoc
            addBooleanOption('Xdoclint:all,-missing', true)
        }
    }

    repositories {
        if (version.endsWith('-SNAPSHOT')) {
            maven {
                name = 'Central Portal Snapshots'
                url = 'https://central.sonatype.com/repository/maven-snapshots/'

                content {
                    includeGroup('io.github.ititus')
                }
            }
        }
        mavenCentral()
    }

    dependencies {
        testImplementation platform('org.junit:junit-bom:6.0.0')
        testImplementation 'org.junit.jupiter:junit-jupiter'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        testImplementation 'org.assertj:assertj-core:3.27.6'
        testImplementation 'org.mockito:mockito-core:5.20.0'
    }

    test {
        useJUnitPlatform()
        testLogging {
            events 'passed', 'skipped', 'failed'
        }
    }
}
